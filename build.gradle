group = "net.burakince"
version = '1.0.0'

apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

project.ext.pluginDesc = [
        id          : 'net.burakince.weblogicDeploymentPlugin',
        version     : project.version,
        goCdVersion : '17.7.0',
        name        : 'Weblogic Deployment Plugin',
        description : 'Web Application Deployer for Weblogic Servers',
        vendorName  : 'Burak Ä°nce',
        vendorUrl   : 'www.burakince.net',
        targetOs    : 'No restrictions'
]

def gitRevision = { ->
    def hashStdOut = new ByteArrayOutputStream()
    exec {
        commandLine "git", "log", "-n", "1", "--format=%H"
        standardOutput = hashStdOut
    }

    return hashStdOut.toString().trim()
}

project.ext.gitRevision = gitRevision()

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '17.7.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    compile 'org.slf4j:slf4j-api:1.7.21'
    testCompile 'junit:junit:4.12'
}

processResources {
    from("src/main/resource-templates") {
        filesMatching('plugin.xml') {
            expand project.pluginDesc
        }
    }
}

jar {
    manifest {
        attributes(
                'Go-Version': project.version,
                'Go-Revision': project.gitRevision,
                'Implementation-Title': project.name,
                'ImplementationVersion': project.version,
                'Implementation-Version': project.version,
                'Source-Compatibility': project.sourceCompatibility,
                'Target-Compatibility': project.targetCompatibility
        )
    }

    from(configurations.compile) {
        into "lib/"
    }
}
